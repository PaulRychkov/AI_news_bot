CREATE or ALTER PROCEDURE dbo.NEWS_AddNews
(
	@HAS_OLD_NEWS INT OUTPUT,
	@JSON_DATA NVARCHAR(MAX)
)
as

SET NOCOUNT ON

DECLARE @JSON_DATA_TABLE table(MESSAGE_ID BIGINT, CHAT_ID BIGINT, MESSAGE_DATE DATETIMEOFFSET, MESSAGE_DATA NVARCHAR(MAX))

INSERT INTO @JSON_DATA_TABLE(MESSAGE_ID, CHAT_ID, MESSAGE_DATE, MESSAGE_DATA)
SELECT MESSAGE_ID, CHAT_ID, MESSAGE_DATE, MESSAGE_DATA
FROM OPENJSON(@JSON_DATA) WITH (
	MESSAGE_ID BIGINT '$.fields.id',
	CHAT_ID BIGINT '$.fields.chat_id',
	MESSAGE_DATE DATETIMEOFFSET '$.fields.date',
	MESSAGE_DATA NVARCHAR(MAX) '$.fields.message_text'
)
set @HAS_OLD_NEWS = 0
IF EXISTS(SELECT 1 FROM dbo.NEWS n 
		  JOIN @JSON_DATA_TABLE j on n.MESSAGE_ID = j.MESSAGE_ID and n.CHAT_ID = j.CHAT_ID)
	set @HAS_OLD_NEWS = 1

INSERT INTO NEWS(MESSAGE_ID, CHAT_ID, MESSAGE_DATE, MESSAGE_DATA)
SELECT j.MESSAGE_ID, j.CHAT_ID, j.MESSAGE_DATE, j.MESSAGE_DATA
FROM @JSON_DATA_TABLE j
LEFT JOIN dbo.NEWS n on n.MESSAGE_ID = j.MESSAGE_ID and n.CHAT_ID = j.CHAT_ID
WHERE n.MESSAGE_ID is null
